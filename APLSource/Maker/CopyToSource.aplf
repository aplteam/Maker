 {toFolder}←{removeZipFiles}CopyToSource(fromFolder projectName);rc;en;more;file;files;subFolder;dir;⎕IO;⎕ML;F;cl
⍝ Copies the contents of `fromFolder` to the source folder specified by `U.GetSourceFolder`.\\
⍝ You can exclude any zip files by specifying the string 'nozip' as left argument.\\
⍝ The function returns either the fully qualified path of the source folder or ⍬ in case
⍝ the user refuses to copy to the source folder.\\
⍝ Do **not** use this function in case the project results just in a single script file.
 ⎕IO←1 ⋄ ⎕ML←1
 :If 1 U.YesOrNo'Copy over to the _Source\ folder?'
     removeZipFiles←'nozip'≡,{0<⎕NC ⍵:⍎⍵ ⋄ ''}'removeZipFiles'
     F←FilesAndDirs
     cl←U.GetCommandLineArgs ⍬
     toFolder←##.Maker.U.GetSourceFolder projectName,'/'
     RecreateFolder toFolder
     files←⊃('recursive' 1)F.Dir fromFolder
     :If removeZipFiles
         files/⍨←{'.zip'≢APLTreeUtils2.Lowercase ¯4↑⍵}¨files       ⍝ Remove all ZIP files
     :EndIf
     dir←''
     :For file :In files
         :If F.IsDir file
             U.Assert rc←'Create!'F.CheckPath toFolder,(⍴fromFolder)↓file
             dir←((⍴fromFolder)↓file),'/'
         :Else
             subFolder←⊃⎕NPARTS(⍴fromFolder)↓file
             file CopyTo toFolder,subFolder
         :EndIf
     :EndFor
 :Else
     toFolder←⎕NULL
 :EndIf
⍝Done
