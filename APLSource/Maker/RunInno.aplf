 {setupFilename}←{path}RunInno name;F;counter;flag;issFilename;inno;cmd;tempdir;iss;batFilename;buff;msg;success;exeName;targetDir;more;rc
⍝ Runs the INNO batch compiler in order to create an installer.\\
⍝ `name` is the project name; for "Fire" that would be "Fire".\\
⍝ Note that the Inno script is modified in case there is an assignment found to `MyAppVersion`: it gets the version number.\\
⍝ Returns the full path of the setup programm created by Inno.\\
⍝ This will work under these assumptions:
⍝ 1. The ISS file is "{name}.iss".
⍝ 2. The ISS file lives in the current directory.\\
⍝ If the Inno ISS file does not exist in the current directory but elsewhere you can specify the path via the (optional) left argument.\\
⍝ Running the Inno compiler this way fails quite frequently, most often with either "file in use" or "EndUpdateResource failed",
⍝ for no apparent reason. However, normally it helps to just try again.
⍝ Normally after 3-5 times it works but sometimes it takes longer, so we try a max of 10 times before giving up.\\
 F←FilesAndDirs
 counter←flag←0
 path←{0<⎕NC ⍵:⍎⍵ ⋄ F.PWD}'path'
 issFilename←'expand'F.NormalizePath path,'\',name,'.iss'
 U.UpdateVersionName issFilename name
 inno←U.SelectInno ⍬
 cmd←'"',inno,'" ',issFilename,'"'
 tempdir←F.EnforceSlash(WinSys.ExpandEnv'%localAppData%'),'\Temp\InnoTempDir'
 'Could not create temp directory'U.Assert'Create!'F.CheckPath tempdir
 iss←1⊃F.NGET issFilename 1
 iss←'^OutputDir *= *\{[^}]*\}'⎕R('OutputDir=',tempdir)⍠('Greedy' 0)⊣iss
 (⊂iss)F.NPUT issFilename 1
 :Repeat
     counter+←1
     ⎕←(⍕counter),' attempt to create the SetUp.exe...'
     batFilename←F.GetTempFilename''
     F.DeleteFile batFilename
     batFilename,←'.bat'
     (⊂cmd)⎕NPUT batFilename
     buff←Execute.Process batFilename
     msg←2⊃buff
     :If 0=+/'The process cannot access the file because it is being used by another process.'⍷∊msg
     :AndIf 0=+/'EndUpdateResource failed'⍷∊msg
         'Inno compiler did abort'U.Assert 0=+/'Compile aborted'⍷∊msg
     :EndIf
     success←∨/'Successful compile'⍷∊msg
     F.DeleteFile batFilename   ⍝ Delete the BAT file
 :Until success∨10=counter
 'Attempted 10 times to create the installer EXE, always failed!'U.Assert 10>counter
 exeName←⊃¯1↑msg
 targetDir←iss U.GetDefFromInno'TargetDir'
 '"TargetDir" needs to be defined in the Inno script'U.Assert 0<≢targetDir
 1 ⎕NDELETE targetDir,⊃,/1↓⎕NPARTS exeName
 targetDir ⎕NMOVE exeName
 setupFilename←F.ExpandPath targetDir,⊃,/1↓⎕NPARTS exeName
⍝Done
