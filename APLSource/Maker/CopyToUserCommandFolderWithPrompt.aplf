 {r}←{toFolder}CopyToUserCommandFolderWithPrompt y;⎕ML;⎕IO;isFolder;path;copyContent;list;toFolder;this;toFolder2;isSubFolder;qdmx;msg
⍝ Copies what's specified by `y` to the user's user command folder (Spice).\\
⍝ `y` might be a simple character vector representing the name of a folder, typically `Dist`.
⍝ In that case the folder itself is copied over.\\
⍝ If you want to copy just the **content** of the folder rather than the folder itself add `\*` at the end.\\
⍝ It might also be a list of files, each one specifying the full or relative path of a file.\\
⍝ Note that any files with the extension `.zip` are removed from `y`.\\
⍝ The target is determined by the result of the function `GetUserCommandFolder`.
⍝ However, you can add a folder to that path be specifying a left argument.
⍝ If you specify, say, `Dist\Fire\` as `y` then the folder `Fire\` will be copied to the target.\\
⍝ If you specify, say, `Dist\Fire\*` as `y` then the **contents** of the folder `Fire\` will be copied to the target.\\
 ⎕IO←1 ⋄ ⎕ML←1
 r←⍬
 :If 0 1∊⍨≡y
 :AndIf isFolder←FilesAndDirs.IsDir(¯1×'*'=¯1↑y)↓y
     path←FilesAndDirs.NormalizePath y
     (('\'=path)/path)←'/'
     copyContent←'*'=¯1↑path
     'Invalid right argument'⎕SIGNAL 11/⍨copyContent∧'/'≠⊃¯2↑path
     list←⊃('recursive' 1)FilesAndDirs.Dir path,(~(¯1↑path)∊'/*')/'/'
     list/⍨←'.zip'∘≢¨APLTreeUtils2.Lowercase 3⊃∘⎕NPARTS¨list
     path←(-'*'=¯1↑path)↓path
     :If 0=⎕NC'toFolder'
         toFolder←(GetUserCommandFolder⊃list),(~copyContent)/'/',⊃¯1↑('/'APLTreeUtils2.Split path)~⊂''
     :Else
         toFolder←(GetUserCommandFolder''),'\',⍕toFolder
     :EndIf
     ⎕←'*** To be copied:'
     ⎕←' ',' ',↑list
     :If 1 U.YesOrNo'Sure that you want to copy these files to ',toFolder,'?'
         'Create!'FilesAndDirs.CheckPath toFolder
         :For this :In list
             isSubFolder←FilesAndDirs.IsDir this
             :If isSubFolder
                 toFolder2←toFolder,'/',isSubFolder/(⍴path)↓this
                 ('Creating ',toFolder2,' failed')U.Assert'Create!'FilesAndDirs.CheckPath toFolder2
             :Else
                 toFolder2←toFolder,'/',(⍴path)↓this
                 this CopyTo toFolder2
             :EndIf
         :EndFor
     :EndIf
 :Else
     list←{{,⊂,⍵}⍣(0 1∊⍨≡⍵)⊣⍵}y
     list/⍨←'.zip'∘≢¨APLTreeUtils2.Lowercase 3⊃∘⎕NPARTS¨list
     toFolder←(GetUserCommandFolder⊃list),'/'
     :Trap 0
         toFolder∘{⍵ CopyTo ⍺}¨list
     :Else
         qdmx←⎕DMX
         :If 0<≢msg←⊃¯1↑qdmx.OSError
             msg ⎕SIGNAL 11
         :Else
             qdmx.EM ⎕SIGNAL 11
         :EndIf
     :EndTrap
 :EndIf
⍝Done
