 {body}←x AddBuildID2Version buildID;⎕ML;⎕IO;version;row;version2;fnsname;ref;rc;buff;offset;length;varsName;line;result;appendix;assignment;flag
 ⍝ `x` can either be a reference pointing to a class, an Interface or a scripted namespace
 ⍝ or the name of a function in an ordinary namespace.\\
 ⍝ Generally `AddBuildID2Version` executes the `Version` function (⍺) if there is
 ⍝ any. It then either adds the build ID (⍵, an integer) if there was not any yet
 ⍝ or replaces it by the built ID given.
 ⎕IO←1 ⋄ ⎕ML←1
 :If flag←0=≡x
     body←⎕SRC ref←x
     result←2⊃ref.Version
     :If 1=≢buff←'∇ *[A-Za-z]←Version'⎕S 0 1 2⍠('Mode' 'L')('Greedy' 0)('IC' 1)⊣body
         (offset length row)←⊃buff
         varsName←{{⍵↑⍨¯1+⍵⍳'←'}' '~⍨⍵↓⍨⍵⍳'∇'}(row+1)⊃body
         row+←2+⊃('\b',varsName,'←')⎕S 2⊣(1+row)↓body
         version←{⍵↑⍨¯1+⍵⍳''''}{{⌽⍵/⍨4>+\''''=⍵}⌽⍵}row⊃body
     :EndIf
 :Else
     ref←⍎¯1↓⊃'.'##.A.SplitPath fnsname←x
     body←⎕NR fnsname
     version←2⊃⍎fnsname
     row←⊃¯1↑##.A.Where∨/¨version∘⍷¨body
 :EndIf
 appendix←{⍵↓⍨+/∧\⍵∊⎕D,'.'}version
 version←(-≢appendix)↓version
 :If 2='.'+.=version
     version2←version,'.',⍕buildID
 :Else
     version2←(⊃'.'##.A.SplitPath version),⍕buildID
 :EndIf
 version2,←appendix
 version←(⊃,/(⊂'\.')@(⍸'.'=version)⊣version),appendix
 line←row⊃body
 line←('''',version,'''')⎕R('''',version2,'''')⊣line
 (row⊃body)←line
 :If flag
     ref.##.⎕FIX body
 :Else
     ref.⎕FX body
 :EndIf
⍝Done
