 {zipFilename}←(VersionFunction CreateZipFile)(name folder);version
⍝ Everything in `folder\` goes into `folder\{name}.zip`.\\
⍝ `VersionFunction` is supposed to be one of:
⍝ 1. A niladic `Version` function that returns either a simple text vector or a nested three-element
⍝    vector with name, version number and version data.
⍝ 2. A simple text vector like '1.2.3' or just ''.\\
⍝ If it is either then the version number  is added as a prefix to the zip filename.\\
⍝ If the `Version` function returns a build number and you don't want
⍝ that to be added to the filename you can specify `({M.DropBuildNumber YourVersionFns})` as operand.
⍝ If you don't want a version number to be added at all then just pass `{''}` as operand.\\
⍝ If you want to specify a string then you must create a function for this, for example:\\
⍝ `⎕FX'r←Version dummy'('r←⍬ '4.3.2 ⍬')`\\
⍝ It is assumed that any function provided as an operand provides either a three-element vector with
⍝ the second element being the actual version number (first=name, third=date) or an empty vector.\\
 :If 0<|1 2⊃⎕AT'VersionFunction'
     version←VersionFunction ⍬
 :Else
     version←VersionFunction
 :EndIf
 zipFilename←name
 :If 1=≡version
     :If 0<≢version
         zipFilename←name,'-',version
     :EndIf
 :ElseIf 0<≢version
     zipFilename←name,'-',2⊃version
 :EndIf
 zipFilename,←{'.zip'/⍨'.zip'≢##.Maker.APLTreeUtils2.Lowercase ¯4↑⍵}zipFilename
 zipFilename←FilesAndDirs.GetTempPath,zipFilename
 1 ⎕NDELETE zipFilename
 folder DotNetZip.ZipFolder zipFilename
 folder ⎕NCOPY⍠('IfExists' 'Replace')⊣zipFilename
⍝Done
