 parms←{XML}GetPublishConfig isDevelopment;XML;needed;⎕ML;⎕IO
⍝ Reads the "publish.config" file and translates it into a namespace with variables.\\
⍝ Note that if `isDevelopment` is 0 then anything that's "needed" but carries an attribute
⍝ `development="true"` is **ignored**!\\
⍝ By defaults the function reads the `publish.config` file in the current directory. Instead
⍝ you can pass the XML as left argument.
 ⎕IO←1 ⋄ ⎕ML←1
 parms←⎕NS''
 XML←{0<⎕NC ⍵:⍎⍵ ⋄ ⎕XML'flat'APLTreeUtils.ReadUtf8File'publish.config'}'XML'
 parms.needed←'needs'xml.GetRangeFrom XML
 :If 0=isDevelopment
     ⍝ Remove what's only needed for development:
     parms.needed⌿⍨←~(⊃¨'development'∘xml.GetAttributeFrom¨parms.needed[;4])∊,¨'true' '1'
 :EndIf
 parms.files←'files'xml.GetRangeFrom XML
 parms.erase←'erase'xml.GetRangeFrom XML
 parms.script←'scriptOnly'xml.GetValueFrom XML
 parms.script←'scriptOnly'xml.GetValueFrom XML
 parms.wsid←'wsid'xml.GetValueFrom XML
 parms.refs←'refs'xml.GetValueFrom XML
 parms.lx←'lx'xml.GetValueFrom XML
⍝Done
