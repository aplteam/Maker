 R←Test_031(stopFlag batchFlag);⎕TRAP;F;wasDir;xml
⍝ Exercise `LoadNeededAndEstablishRefs` assuming development (]OpenMyProject)
⍝ Because this loads the `Tester` class we MUST NOT load into # as this would overwrite the code
⍝ of Tester when the `Maker` test cases are executed!
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 F←##.Maker.FilesAndDirs
 #.⎕SHADOW'Foo'
 'Foo.Foo'#.⎕NS''
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.Foo.Foo.Hello←{'World'}
 wasDir←F.Cd'MoreTests'
 xml←##.Maker.GetPublishConfig 1

 ⍝ ↓↓↓ We add `DRC` from the "Conga" workspace ↓↓↓
 xml.needed⍪←2 'ws' 'Conga'(↑('lib' '[DYALOG]')('copy' 'DRC'))5

 #.TEMP ##.Maker.LoadNeededAndEstablishRefs'Foo'xml
 →T.GoToTidyUp 3≠≢#.Foo.⎕NL 9.1                  ⍝ `Foo` and `DRC`
 →T.GoToTidyUp 4≠≢#.Foo.⎕NL 9.4                  ⍝ `APLTreeUtils2`, `FilesAndDirs`, `OS`, `Tester` and `WinReg`
 →T.GoToTidyUp 1≠≢#.Foo.Foo.⎕NL⍳16

 R←T._OK

∆TidyUp:
 {}F.Cd wasDir
⍝Done
