 R←Test_003(stopFlag batchFlag);⎕TRAP;F;folder;zipFilename;rc;targetFolder;list1;list2
⍝ Exercise `CopyToSource`.
⍝ Needs the user.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 :If batchFlag
     R←T._NoBatchTest
     :Return
 :EndIf

 F←##.Maker.FilesAndDirs

 folder←'./Tests/'
 zipFilename←folder,'myzipfile.zip'
 rc←##.APLTreeUtils.WriteUtf8File zipFilename'Pretends to be a zip file'
 →T.GoToTidyUp 0≠≢rc
 targetFolder←##.Maker.CopyToSource'./Tests/' 'Maker_Test_003'

 list1←⊃('recursive' 1)F.Dir'./Tests/'
 list1←(⍴'expand'F.NormalizePath'./Tests/')↓¨'expand'∘F.NormalizePath¨list1
 list2←(⍴##.Maker.U.GetSourceFolder'Maker_Test_003/')↓¨⊃('recursive' 1)F.Dir targetFolder
 →T.GoToTidyUp list1≢list2

 R←T._OK

∆TidyUp:
 F.RmDir targetFolder
⍝Done
