 R←Test_001(stopFlag batchFlag);⎕TRAP;F;path;zipFilename1;zipFilename2;zipFilename3
⍝ Exercise `CreateZipFile`
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 F←##.Maker.FilesAndDirs

 path←F.GetTempFilename2''
 F.DeleteFile path
 path←¯4↓path
 Assert'Create!'F.CheckPath path
 (⊂'this')⎕NPUT path,'\file1.txt'
 (⊂'that')⎕NPUT path,'\file2.txt'
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.⎕FX'r←Version' 'r←''Foo'' ''0.1.0'' ''2019-02-19'''
 zipFilename1←(#.TEMP.Version ##.Maker.CreateZipFile'Foo'path)
 →T.GoToTidyUp(F.CurrentSep,'Foo-0.1.0.zip'){⍺≢(-≢⍺)↑⍵}zipFilename1
 zipFilename2←({⍬}##.Maker.CreateZipFile'Foo'path)
 →T.GoToTidyUp(F.CurrentSep,'Foo.zip'){⍺≢(-≢⍺)↑⍵}zipFilename2
 zipFilename3←({⍬}##.Maker.CreateZipFile'Foo.zip'path)
 →T.GoToTidyUp(F.CurrentSep,'Foo.zip'){⍺≢(-≢⍺)↑⍵}zipFilename3

 R←T._OK

∆TidyUp:
 :Trap 0 ⋄ F.RmDir path ⋄ :EndTrap
⍝Done
