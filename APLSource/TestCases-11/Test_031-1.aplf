 R←Test_031(stopFlag batchFlag);⎕TRAP;F;wasDir;xml
⍝ Exercise `LoadNeededAndEstablishRefs` assuming development (]OpenMyProject)
⍝ Because this loads the `Tester` class we MUST NOT load into # as this would overwrite the code
⍝ of Tester when the `Maker` test cases are executed!
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 F←##.Maker.FilesAndDirs
 #.⎕SHADOW'_Foo'
 '_Foo.Foo'#.⎕NS''
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #._Foo.Foo.Hello←{'World'}
 wasDir←F.Cd'MoreTests'
 xml←##.Maker.GetPublishConfig 1

 ⍝ ↓↓↓ We add `DRC` from the "Conga" workspace ↓↓↓
 xml.needed⍪←2 'ws' 'Conga'(↑('lib' '[DYALOG]')('copy' 'DRC'))5

 #.TEMP ##.Maker.LoadNeededAndEstablishRefs'Foo'xml
 →GoToTidyUp 3≠≢#._Foo.⎕NL 9.1                  ⍝ `Foo`, `APLTreeUtils` and `DRC`
 →GoToTidyUp 4≠≢#._Foo.⎕NL 9.4                  ⍝ `FilesAndDirs`, `OS`, `Tester` and `WinReg`
 →GoToTidyUp 1≠≢#._Foo.Foo.⎕NL⍳16

 R←∆OK

∆TidyUp:
 {}F.Cd wasDir
⍝Done
