 {zipFilename}←(VersionFunction CreateZipFile)(name folder);list;has;buff;rc;more;myZipper;was;version;F
⍝ Everything in `folder\` goes into `folder\{name}.zip`.\\
⍝ `VersionFunction` is supposed to be a `Version` function. If it is then the version is added as a prefix to the zip filename.\\
⍝ If you don't want that then pass `{''}` as operand.\\
⍝ If you want to specify a string then you must create a function for this, for example:\\
⍝ `⎕FX'r←Version dummy'('r←⍬ '4.3.2 ⍬')`\\
⍝ See below why that is necessary.\\
⍝ It is assumed that any function provided as an operand provides either a three-element vector with the second element
⍝ being the actual version number (first=name, third=date) or an empty vector.\\
⍝ **Warning**: this function does temporarily change the current directory. This is necessary because otherwise the relative
⍝ paths created by "SevenZip" would not be what they are supposed to be.
 F←FilesAndDirs
 was←F.Cd folder
 :Trap 0
     :If 0<|1 2⊃⎕AT'VersionFunction'
         buff←VersionFunction ⍬
     :Else
         buff←VersionFunction
     :EndIf
     :If 0<≢buff
         zipFilename←name,'-',2⊃buff
     :Else
         zipFilename←name
     :EndIf
     zipFilename,←{'.zip'/⍨'.zip'≢##.Maker.APLTreeUtils.Lowercase ¯4↑⍵}zipFilename
     myZipper←⎕NEW SevenZip(,⊂zipFilename)
     list←2↓¨⊃('recursive' 1)F.Dir'.\*'
     list/⍨←F.IsFile list
     :Repeat
         (rc more buff)←myZipper.Add list
         has←2⊃myZipper.List ⍬
     :Until 0∊⍴list←list~has
 :Else
     {}F.Cd was          ⍝ Make sure that the current dir is restored even if the user Ctrl+Breaks the code!
     (1+⊃⎕LC)⎕STOP⊃⎕SI
     :Return
 :EndTrap
 {}F.Cd was
⍝Done
