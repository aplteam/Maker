 {r}←{folder}UpdateVersionNumberInInno_ISS projectName;iss;row;⎕IO;⎕ML;A;inno;filename
⍝ This function updates the version number in any INNO `.ISS` file.\\
⍝ INNO is the installer software used for creating installer EXEs for applications on GitHub that carry the "apl-cation" topic.\\
⍝ This function shall be called only after the workspace was created by calling `MakeWorkspace`.\\
⍝ * `projectName` is the name of the ISS file
⍝ * `←` is always `⍬`\\
⍝ The optional left argument is normally used by test cases only because the INNO file is expected to be on the project's main level.
⍝ The function reads the ISS file of the given application, changes the variable `MyAppVersion` to what the `Version` function
⍝ of the given application defines and writes it back to disk.
 ⎕IO←1 ⋄ ⎕ML←1
 r←⍬
 A←APLTreeUtils
 folder←{0<⎕NC ⍵:⍎⍵ ⋄ ''}'folder'
 filename←folder,projectName{⍺,⍵/⍨⍵≢A.Lowercase(-⍴⍵)↑⍺}'.iss'
 iss←A.ReadUtf8File filename
 row←A.Where∨/¨'#define MyAppVersion'∘⍷¨iss
 (row⊃iss)←'#define MyAppVersion "',(2⊃#.⍎projectName,'.Version'),'"'
 A.WriteUtf8File filename iss
⍝Done
